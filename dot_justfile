set unstable := true

@_default:
    just --list

# we want to run flake commands in the source directory
# since they might update the flake.lock file

flakes_source_prefix := shell('chezmoi source-path') + "/dot_config/exact_"
flakes_prefix := "~/.config/"
switch := 'switch'
ppc_path := which('powerprofilesctl')
original_prof := if ppc_path != '' { shell('powerprofilesctl get') } else { '' }

_boost:
    {{ if ppc_path != '' { 'powerprofilesctl set performance' } else { '' } }}

_unboost:
    {{ if ppc_path != '' { 'powerprofilesctl set ' + original_prof } else { '' } }}

hswitch: _boost && _unboost
    home-manager switch --flake {{ flakes_prefix }}home-manager

[linux]
add pkg: && hswitch
    sed -i '/SED_ADD_PKGS_HERE/a\          {{ pkg }}' {{ flakes_source_prefix }}home-manager/home.nix
    chezmoi apply

[macos]
add pkg: && hswitch
    sed -i '' $'s/SED_ADD_PKGS_HERE/&\\\n          {{ pkg }}/' {{ flakes_source_prefix }}home-manager/home.nix
    chezmoi apply

[linux]
switch switch=(switch): _boost && _unboost
    sudo nixos-rebuild {{ switch }} --flake {{ flakes_prefix }}nix#nixos

[macos]
switch switch='switch':
    # doesn't work as sudo
    darwin-rebuild {{ switch }} --flake {{ flakes_prefix }}nix-darwin#darwin

prune: _prune-nix _prune-docker

_prune-nix:
    sudo nix-collect-garbage -d
    sudo nix-collect-garbage
    nix-store --optimise

_prune-docker:
    podman container prune 
    podman image prune -a --external

[macos]
_update-flake:
    nix flake update --flake {{ flakes_prefix }}nix-darwin

[linux]
_update-flake:
    nix flake update --flake {{ flakes_prefix }}nix

_update-home-flake:
    nix flake update --flake {{ flakes_prefix }}home-manager

_update-brew:
    brew update
    brew upgrade

_update-dotfiles:
    chezmoi update

[linux]
update: _update-dotfiles _update-home-flake hswitch _update-flake switch

[macos]
update: _update-dotfiles _update-brew _update-home-flake hswitch _update-flake switch
